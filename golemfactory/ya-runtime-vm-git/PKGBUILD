# Maintainer: Wilson E. Alvarez <wilson.e.alvarez1@gmail.com>

_pkgname=ya-runtime-vm
pkgname=${_pkgname}-git
pkgdesc="Golemn network Docker-like runtime environment for Linux systems"
pkgver=0.2.3.r1.gf9f2521
pkgrel=1
arch=('x86_64')
url="https://www.golem.network/"
license=('GPL')
makedepends=('rust' 'musl')
source=("git+https://github.com/golemfactory/${_pkgname}.git"
	)
sha256sums=('SKIP')
provides=( "${_pkgname}" )

pkgver()
{
	cd "${srcdir}/${_pkgname}"
	( set -o pipefail
	 git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
	 printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build()
{
	cd "${srcdir}/${_pkgname}"
	cargo build --release
}

package()
{
	cd "${srcdir}/${_pkgname}"
	mkdir -p "${pkgdir}/usr/bin"
	install -Dm 755 "target/release/gvmkit" "${pkgdir}/usr/bin/gvmkit"
	install -Dm 755 "target/release/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
}
