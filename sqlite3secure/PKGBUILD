# Maintainer: Wilson E. Alvarez <wilson.e.alvarez1@gmail.com>

pkgname='sqlite3secure'
_sqlite_srcver=3180000
pkgver=3.18
pkgrel=1
pkgdesc="wxsqlite3 is an SQLite3 encryption extension which has been compiled along with SQLite3 for an interactive shell."
arch=('i686' 'x86_64')
license=('custom:Public Domain' 'custom:wxWindows Library Licence, Version 3.1')
url="https://github.com/utelle/wxsqlite3"
makedepends=('tcl' 'readline')

# SQLite and wxSQLite versions are tied. Update these variables to the right versions as we update the PKGBUILD:
_year=2017
_wxsqlite_version="v3.5.3"

source=("http://sqlite.org/${_year}/sqlite-amalgamation-${_sqlite_srcver}.zip"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/codec.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/codec.h"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/codecext.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/csv.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/extensionfunctions.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/rijndael.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/rijndael.h"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/sha2.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/sha2.h"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/sqlite3secure.c"
"https://raw.githubusercontent.com/utelle/wxsqlite3/${_wxsqlite_version}/sqlite3/secure/src/userauth.c"
"sqlite3license.txt"
"wxsqlitelicense.txt"
)
options=('!emptydirs' '!makeflags') # json extensions breaks parallel build
sha256sums=(
"1cfb91106650a12840e107db29f2ec4afe3d7f9dc5936770ef53bc3eb6387b6e"
"e423af8d7a4df819c1dd9e9fc0a0bc32ce077ba892ac55134bd1d4eb7c5b4688"
"adaaf5ed801b6adeff38ac69e99b8a84cc8bdbc4012559b9ed85a2c9f796302e"
"20d0c42485396f17c4a3ac5411b77a5dcb8cd516ad00682b1e183150e4260254"
"1acdcd41484349f667568b36d511ff4832107cf5d932f69478c570965247bad0"
"818cebdde638c5e5eb2404cebeb0be6c605585218667b732efae6de30f478c26"
"6b5dc3bcc8f6fd41d4af0cf471dca0f816bd42242ad1fc59bff6119f8fddcd6d"
"e9bf346d5b61bb3201f0993f178a5c226ffd15a825d7a7537524a5a983c0ccc7"
"0be45ed49d66b9e18b0da0685d8f052bc1d139b6ce8da7c6f9c6c181ca594be8"
"856bbb9991cd526a1c973f6307270abae7c15529e3a9b773474754bd1d69e6b4"
"f4c846d96f3e03397d9b73660360fd6263149226556f313a139f8aba56c2603f"
"214eda752756b33b32550203ff820dc673469a68229797b0da6f5fec90e66fa5"
"4e57d9ac979f1c9872e69799c2597eeef4c6ce7224f3ede0bf9dc8d217b1e65d"
"655a2d832711d5e8f4eb318c56ca01c076937f05c0f9b532714e57811f4e9e4a"
)

prepare(){
	mv codec.c sqlite-amalgamation-${_sqlite_srcver}
	mv codec.h sqlite-amalgamation-${_sqlite_srcver}
	mv codecext.c sqlite-amalgamation-${_sqlite_srcver}
	mv csv.c sqlite-amalgamation-${_sqlite_srcver}
	mv extensionfunctions.c sqlite-amalgamation-${_sqlite_srcver}
	mv rijndael.c sqlite-amalgamation-${_sqlite_srcver}
	mv rijndael.h sqlite-amalgamation-${_sqlite_srcver}
	mv sha2.c sqlite-amalgamation-${_sqlite_srcver}
	mv sha2.h sqlite-amalgamation-${_sqlite_srcver}
	mv sqlite3secure.c sqlite-amalgamation-${_sqlite_srcver}
	mv userauth.c sqlite-amalgamation-${_sqlite_srcver}
}

build() {
	#Note: -DCODEC_TYPE=CODEC_TYPE_AES128 is enabled by default but we
	#		explicitly define CODEC_TYPE that way in order to avoid future headaches
	export CPPFLAGS="$CPPFLAGS -DHAVE_READLINE \
		-DCODEC_TYPE=CODEC_TYPE_AES128 \
		-DSQLITE_CORE \
		-DSQLITE_ENABLE_COLUMN_METADATA \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1 \
		-DSQLITE_ENABLE_FTS3 \
		-DSQLITE_ENABLE_FTS3_PARENTHESIS \
		-DSQLITE_ENABLE_FTS4 \
		-DSQLITE_ENABLE_FTS5 \
		-DSQLITE_ENABLE_JSON1 \
		-DSQLITE_ENABLE_RTREE \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY \
		-DSQLITE_HAS_CODEC \
		-DSQLITE_SECURE_DELETE \
		-DSQLITE_SOUNDEX \
		-DTHREADSAFE=1 \
		-DWXSQLITE3_HAVE_METADATA \
		$CXXFLAGS"

	# Uncomment to use 256-bit AES encryption instead. 							 
	#export CPPFLAGS+="-DCODEC_TYPE=CODEC_TYPE_AES256"

	cd sqlite-amalgamation-${_sqlite_srcver}
	gcc ${CPPFLAGS} -Wall  -ldl  -lpthread  -lm -ledit shell.c sqlite3secure.c \
		-o sqlite3secure
}

package() {
	install -Dm 755 ${srcdir}/sqlite-amalgamation-${_sqlite_srcver}/sqlite3secure ${pkgdir}/usr/bin/sqlite3secure
}
